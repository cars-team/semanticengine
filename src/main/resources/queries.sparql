CONSTRUCT {
    ?newObservation a sosa:Observation ;
        sosa:madeBySensor <sensor/virtual/room/1/people> ;
        sosa:resultTime ?now ;
        sosa:observedProperty <room/1/people> ;
        sosa:hasFeatureOfInterest <room/1> ;
        sosa:hasSimpleResult ?newResult .
} WHERE {
    {
        SELECT ?observation WHERE {
            ?observation a sosa:Observation ;
                sosa:observedProperty ?propToObserve ;
                sosa:resultTime ?resultTime .
            {
                SELECT ?propToObserve ?obsTime WHERE {
	                <http://gauss.it/museum/observations/room/1/door/1/presenceOnDoorInside#2017-04-16T00:00:00+00:00> sosa:observedProperty ?obsProp ;
                        sosa:resultTime ?obsTime .
                    BIND(STR(?obsProp) as ?obsPropStr)
	                BIND(IRI(IF(STRENDS(?obsPropStr, "Inside"), CONCAT(SUBSTR(?obsPropStr, 1, STRLEN(?obsPropStr)-STRLEN("Inside")), "Outside"), CONCAT(SUBSTR(?obsPropStr, 1, STRLEN(?obsPropStr)-STRLEN("Outside")), "Inside"))) as ?propToObserve)
                }
            }
            BIND(?resultTime - ?obsTime as ?timeDifference)
            FILTER(?resultTime > ?obsTime)
            FILTER(?timeDifference < "PT1.500S"^^xsd:duration)
        }
    }
    {
        SELECT ?otherResult WHERE {
            {
                ?otherObservation a sosa:Observation ;
                    sosa:madeBySensor <sensor/virtual/room/1/people> ;
                    sosa:resultTime ?otherTime ;
                    sosa:hasSimpleResult ?otherResult .
            } UNION {
                BIND("0"^^xsd:integer as ?otherResult)
                BIND("1970-01-01T00:00:00+00:00"^^xsd:dateTime as ?otherTime)
            }
        } ORDER BY DESC(?otherTime) LIMIT 1
    }
    BIND(xsd:integer(?otherResult)+1 as ?newResult)
    BIND(now() as ?now)
    BIND(IF(BOUND(?observation), IRI(CONCAT("sensor/virtual/room/1/people#", STR(?now))), ?observation) as ?newObservation) # ?newObservation is bound only if the inner select finds at least one observation, otherwise it's not bound and the insert will do nothing
}

INSERT {
    <http://gauss.it/museum/sensor/virtual/room/1/people#2018-09-11T03:20:20.804+02:00> a sosa:Observation ;
	sosa:hasFeatureOfInterest  <http://gauss.it/museum/room/1> ;
	sosa:hasSimpleResult       1 ;
	sosa:madeBySensor          <http://gauss.it/museum/sensor/virtual/room/1/people> ;
	sosa:observedProperty      <http://gauss.it/museum/room/1/people> ;
	sosa:resultTime            "2018-09-11T03:20:20.804+02:00"^^xsd:dateTime .
} WHERE {
	BIND(now() as ?now)
}
